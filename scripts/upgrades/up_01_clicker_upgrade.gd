class_name Up01ClickerUpgrade
extends Node
## Upgrade 01 - Clicker Upgrade
##
## Increase the amount of stardust generated by the clicker.


signal leveled_up


## Current level of the upgrade.
var level : int = 0
## Base cost of the upgrade.
var base_cost : int = 5


## Load level from data.
func _init() -> void:
	level = Game.ref.data.up_01_level


## Returns the title of the upgrade.
func title() -> String:
	return "Clicker Upgrade"


## Returns the description of the upgrade.
func description() -> String:
	var new_description : String = "Increase the amount of stardust created by the Clicker."
	new_description += "\n[b]Cost : [/b] %s" %cost()
	return new_description


## Returns the cost of the upgrade.
func cost() -> int:
	var new_cost : int = base_cost * pow(1.5, level)
	return new_cost


func level_up() -> void:
	var error : Error = HandlerStardust.ref.consume_stardust(cost())
	if not error:
		
		level += 1
		Game.ref.data.up_01_level = level
		leveled_up.emit()


func can_afford() -> bool:
	if HandlerStardust.ref.stardust() >= cost():
		return true
		
	return false
